// ---------------------------------------------------------------------
// SHAPESBrushAddMenuItem.mel
//
// Creates a menu item for the SHAPES Brush tool in the default Maya
// modify menu.
//
// ---------------------------------------------------------------------

global proc SHAPESBrushDeleteMenuItems()
{
    deleteUI SHAPESBrushMenuDivider;
    deleteUI SHAPESBrushMenuItem;
    deleteUI SHAPESBrushOptionMenuItem;
}


proc int mayaVersion()
{
    int $version = `about -apiVersion` / 100.0;
    if ($version > 10000)
        $version = $version / 100;
    return $version;
}


global proc SHAPESBrushAddMenuCommand()
{
    global string $gMainModifyMenu;

    if (`menu -ex $gMainModifyMenu`)
    {
        string $current = `menu -query -postMenuCommand $gMainModifyMenu`;
        string $cmd = "SHAPESBrushCreateMenuItems; " + $current;
        menu -edit -postMenuCommand $cmd $gMainModifyMenu;
    }
}


proc addMenuItems()
{
    string $ann = "Sculpting brush for blend shape targets";

    menuItem -label "SHAPES Brush Tool"
             -annotation $ann
             -image "SHAPESBrush.svg"
             -command "SHAPESBrushToolCtx"
             SHAPESBrushMenuItem;
    menuItem -optionBox 1
             -annotation $ann
             -image "SHAPESBrush.svg"
             -command "SHAPESBrushToolCtx; toolPropertyWindow;"
             SHAPESBrushOptionMenuItem;
}


global proc SHAPESBrushCreateMenuItems()
{
    global string $gMainModifyMenu;

    if (!`pluginInfo -query -loaded SHAPESBrush`)
        loadPlugin SHAPESBrush;

    int $version = `mayaVersion`;
    string $menu = $gMainModifyMenu;

    if (!`menu -query -numberOfItems $menu`)
        ModObjectsMenu $menu;

    setParent -menu $menu;
    string $div = "";
    if ($version >= 2015)
        $div = "-dividerLabel \"SHAPES Brush\" ";
    eval("menuItem -divider 1 " + $div + "SHAPESBrushMenuDivider");

    addMenuItems;

    // Replace the command attached to the menu with the Maya default
    // command to update the menu content.
    string $current = `menu -query -postMenuCommand $gMainModifyMenu`;
    string $cmd = substitute("SHAPESBrushCreateMenuItems; ", $current, "");
    menu -edit -postMenuCommand $cmd $gMainModifyMenu;
}

// ---------------------------------------------------------------------
// MIT License
//
// Copyright (c) 2020 Ingo Clemens, brave rabbit
// SHAPESBrush is under the terms of the MIT License
//
// Permission is hereby granted, free of charge, to any person obtaining
// a copy of this software and associated documentation files (the
// "Software"), to deal in the Software without restriction, including
// without limitation the rights to use, copy, modify, merge, publish,
// distribute, sublicense, and/or sell copies of the Software, and to
// permit persons to whom the Software is furnished to do so, subject to
// the following conditions:
//
// The above copyright notice and this permission notice shall be
// included in all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
// EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
// MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
// IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
// CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
// TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
// SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
//
// Author: Ingo Clemens    www.braverabbit.com
// ---------------------------------------------------------------------
